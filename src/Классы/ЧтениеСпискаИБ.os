Перем Результат;
Перем Параметры;

#Область ПрограммныйИнтерфейс

// Функция - возвращает тип файла, обрабатываемый данной обработкой
// 
// Возвращаемое значение:
//  Строка -  тип файла, обрабатываемый данной обработкой
//
Функция ТипФайла() Экспорт
	
	Возврат Перечисления.ТипыФайлов.НастройкиКластера;
	
КонецФункции // ТипФайла()

// Функция - возвращает результат, накопленный обработкой
// 
// Возвращаемое значение:
//  Произвольный -  результат, накопленный обработкой
//
Функция ПолучитьРезультат() Экспорт
	
	Возврат Результат;
	
КонецФункции // ПолучитьРезультат()

// Процедура - проверяет, что элемент является записью ИБ файла настроек кластера
// и добавляет его в список информационных баз
//
// Параметры:
//	Элемент         - Структура       проверяемый элемент
//		*Родитель            - Структура                 - ссылка на элемент-родитель
//		*Уровень             - Число                     - уровень иерархии элемента
//		*Индекс              - Число                     - индекс элемента в массиве значений родителя
//		*НомераСтрок         - Соответсвие(Число)        - массив номеров строк из которых был прочитан элемент и его дочерние элементы
//		*Значения            - Массив(Структура)         - массив дочерних элементов
//
Процедура ДобавитьЗапись(Элемент) Экспорт
	
	Если НЕ Элемент.Уровень = 4 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Элемент.Родитель.Индекс = 12 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Элемент.Родитель.Родитель.Индекс = 1 Тогда
		Возврат;
	КонецЕсли;
	
	Запись = Новый Структура();
	Запись.Вставить("Ид", Элемент.Значения[0]);
	Запись.Вставить("Имя", Служебный.ОбработатьКавычкиВСтроке(Элемент.Значения[1]));
	Запись.Вставить("Описание", Служебный.ОбработатьКавычкиВСтроке(Элемент.Значения[2]));
	Запись.Вставить("ТипСУБД", Служебный.ОбработатьКавычкиВСтроке(Элемент.Значения[3]));
	Запись.Вставить("СерверСУБД", Служебный.ОбработатьКавычкиВСтроке(Элемент.Значения[4]));
	Запись.Вставить("ИмяБазыСУБД", Служебный.ОбработатьКавычкиВСтроке(Элемент.Значения[5]));
	Запись.Вставить("ПользовательСУБД", Служебный.ОбработатьКавычкиВСтроке(Элемент.Значения[6]));
	
	ПараметрыИБ = СтрРазделить(Служебный.ОбработатьКавычкиВСтроке(Элемент.Значения[8]), ";");
	Для Каждого ТекПараметр Из ПараметрыИБ Цикл
		ОписаниеПараметра = СтрРазделить(ТекПараметр, "=");
		Запись.Вставить(ОписаниеПараметра[0], ОписаниеПараметра[1]);
	КонецЦикла;
	
	Если НЕ ТипЗнч(Результат) = Тип("Массив") Тогда
		Результат = Новый Массив();
	КонецЕсли;
	
	Результат.Добавить(Запись);
	
КонецПроцедуры // ДобавитьЗапись()

// Функция - проверяет, что элемент является записью ИБ файла настроек кластера
// и проверяет необходимость удаления элемента
//
// Параметры:
//	Элемент                  - Структура          - проверяемый элемент (см. НужноУдалитьЭлемент)
//		*Родитель            - Структура                 - ссылка на элемент-родитель
//		*Уровень             - Число                     - уровень иерархии элемента
//		*Индекс              - Число                     - индекс элемента в массиве значений родителя
//		*НомераСтрок         - Соответсвие(Число)        - массив номеров строк из которых был прочитан элемент и его дочерние элементы
//		*Значения            - Массив(Структура)         - массив дочерних элементов
//
// Возвращаемое значение:
//   Булево - Истина - элемент нужно удалить после обработки
//
Функция НужноУдалитьЭлемент(Элемент) Экспорт
	
	Если НЕ Элемент.Уровень = 4 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ Элемент.Родитель.Индекс = 12 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ Элемент.Родитель.Родитель.Индекс = 1 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // НужноУдалитьЭлемент()

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - Обработчик события формы "ПриСозданииОбъекта"
//
// Параметры:
//	Чтение                - ЧтениеСкобкоФайла  - Объект чтения скобкофайла из которого выполняется вызов обработчика
//	ПараметрыОбработки    - Структура          - дополнительные параметры обработки
//
Процедура ПриСозданииОбъекта(Чтение, ПараметрыОбработки = Неопределено)
	
	ЧтениеСкобкофайла = Чтение;

	Параметры = Новый Структура();

	Если ТипЗнч(ПараметрыОбработки) = Тип("Структура") Тогда
		Параметры = ПараметрыОбработки;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииОбъекта()

#КонецОбласти
