&НаКлиенте
Перем КаталогТекущейОбработки;

#Область ОбработчикиЭлементовФормы

// Процедура - обработка начала выбора файла
//
&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "Настройки чтения данных (*.json)|*.json";
	Диалог.Заголовок = "Настройки чтения данных";

	ЗавершениеВыбораФайла = Новый ОписаниеОповещения("ПутьКФайлуНачалоВыбораЗавершение", ЭтаФорма);
	
	Диалог.Показать(ЗавершениеВыбораФайла);
	
КонецПроцедуры // ПутьКФайлуНачалоВыбора()

// Процедура - продолжение обработки выбора файла
//
&НаКлиенте
Процедура ПутьКФайлуНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(ВыбранныеФайлы) = Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлуНастроек = ВыбранныеФайлы[0];
	
КонецПроцедуры // ПутьКФайлуНачалоВыбораЗавершение()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды "ОбработатьДанные" на сервере
//
&НаСервере
Процедура ОбработатьДанныеНаСервере(Знач ПараметрыОбработкиДанных)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ОбработкаОбъект.УстановитьПараметрыОбработкиДанных(ПараметрыОбработкиДанных);
	
	ОбработкаОбъект.ОбработатьДанные();
	
КонецПроцедуры // ОбработатьДанныеНаСервере()

// Процедура - обработчик команды "ОбработатьДанные"
//
&НаКлиенте
Процедура ОбработатьДанные(Команда)
	
	ПараметрыОбработкиДанных = ПрочитатьПараметрыОбработкиДанных();
	
	ОбработатьДанныеНаСервере(ПараметрыОбработкиДанных);
	
КонецПроцедуры // ОбработатьДанные()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция - читает и возвращает параметры обработки данных из двоичных данных во временном хранилище
//
// Параметры:
//  АдресПараметров            - Строка                 - адрес двоичных данных параметров во временном хранилище
//
// Возвращаемое значение:
//  Структура                  - настройки обработки данных
//       *ПутьКОбработке       - Строка                 - путь к файлу внешней обработке
//       *ПроцедураОбработки   - Строка                 - имя процедуры обработки данных
//       *Параметры            - Строка                 - структура параметров процедуры обработки данных
//           *<Имя параметра>  - Произвольный           - знаечние параметра процедуры обработки данных
//       *ИмяОбработки         - Строка                 - имя внешней обработки после подключения
//       *Обработчики          - Массив(Структура)      - массив обработчиков данных,
//                                                        полученных от обработки текущего уровня
//                                                        (состав полей элемента аналогичен текущему уровню) 
//
&НаСервере
Функция ПрочитатьПараметрыОбработкиДанныхНаСервере(АдресПараметров)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	Возврат ОбработкаОбъект.ПрочитатьПараметрыОбработкиДанных(ПолучитьИзВременногоХранилища(АдресПараметров));
	
КонецФункции // ПрочитатьПараметрыОбработкиДанныхНаСервере()

// Функция - читает и возвращает параметры обработки данных из файла JSON, указанного в поле "ПутьКФайлу"
//
// Возвращаемое значение:
//  Структура                  - настройки обработки данных
//       *ПутьКОбработке       - Строка                 - путь к файлу внешней обработке
//       *ПроцедураОбработки   - Строка                 - имя процедуры обработки данных
//       *Параметры            - Строка                 - структура параметров процедуры обработки данных
//           *<Имя параметра>  - Произвольный           - знаечние параметра процедуры обработки данных
//       *АдресОбработки       - Строка                 - адрес внешней обработки во временном хранилище
//                                                        (заполняется в результате выполнения процедуры)
//       *ИмяОбработки         - Строка                 - имя внешней обработки после подключения
//       *Обработчики          - Массив(Структура)      - массив обработчиков данных,
//                                                        полученных от обработки текущего уровня
//                                                        (состав полей элемента аналогичен текущему уровню) 
//
&НаКлиенте
Функция ПрочитатьПараметрыОбработкиДанных()
	
	АдресПараметров = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПутьКФайлуНастроек),
	                                                ЭтотОбъект.УникальныйИдентификатор);
	
	ПараметрыОбработкиДанных = ПрочитатьПараметрыОбработкиДанныхНаСервере(АдресПараметров);
	
	ПодключитьВнешниеОбработки(ПараметрыОбработкиДанных);
	
	Возврат ПараметрыОбработкиДанных;
	
КонецФункции // ПрочитатьПараметрыОбработкиДанных()

// Процедура - рекурсивно подключает внешние обработки,
// указанные в настройках обработки данных
//
// Параметры:
//  ПараметрыОбработкиДанных   - Массив(Структура)      - настройки обработки данных
//       *ПутьКОбработке       - Строка                 - путь к файлу внешней обработке
//       *ПроцедураОбработки   - Строка                 - имя процедуры обработки данных
//       *Параметры            - Строка                 - структура параметров процедуры обработки данных
//           *<Имя параметра>  - Произвольный           - знаечние параметра процедуры обработки данных
//       *АдресОбработки       - Строка                 - адрес внешней обработки во временном хранилище
//                                                        (заполняется в результате выполнения процедуры)
//       *ИмяОбработки         - Строка                 - имя внешней обработки
//                                                        (заполняется в результате выполнения процедуры)
//       *Обработчики          - Массив(Структура)      - массив обработчиков данных,
//                                                        полученных от обработки текущего уровня
//                                                        (состав полей элемента аналогичен текущему уровню) 
//
&НаКлиенте
Процедура ПодключитьВнешниеОбработки(ПараметрыОбработкиДанных)
	
	Если ТипЗнч(ПараметрыОбработкиДанных) = Тип("Структура") Тогда
		Для Каждого ТекЭлемент Из ПараметрыОбработкиДанных Цикл
			Если ТекЭлемент.Ключ = "ПутьКОбработке" Тогда
				ИмяОбработки = "";
				АдресОбработки = "";
				ПараметрыОбработкиДанных.Свойство("ИмяОбработки", ИмяОбработки);
				АдресОбработки =
					ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПараметрыОбработкиДанных.ПутьКОбработке),
				                                  ЭтотОбъект.УникальныйИдентификатор);
			Иначе
				ПодключитьВнешниеОбработки(ТекЭлемент.Значение);
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(ИмяОбработки) Тогда
			ПараметрыОбработкиДанных.Вставить("ИмяОбработки", ИмяОбработки);
		КонецЕсли;
		Если ЗначениеЗаполнено(АдресОбработки) Тогда
			ПараметрыОбработкиДанных.Вставить("АдресОбработки", АдресОбработки);
		КонецЕсли;
	ИначеЕсли ТипЗнч(ПараметрыОбработкиДанных) = Тип("Массив") Тогда
		Для Каждого ТекЭлемент Из ПараметрыОбработкиДанных Цикл
			ПодключитьВнешниеОбработки(ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры // ПодключитьВнешниеОбработки()

#КонецОбласти
